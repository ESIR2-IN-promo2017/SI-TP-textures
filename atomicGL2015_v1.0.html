<html>
<head>
	<title>atomicGL DEBUG </title>
	<meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">

	<script type="text/javascript" src="gltp1_fichiers/glMatrix-0.js"></script>
	<script type="text/javascript" src="gltp1_fichiers/webgl-utils.js"></script>

	<!-- ATOMICGL ENGINE -->
	<!-- load "objects" -->
	<script type="text/javascript" src="objs/maison3.js"></script>
	<script type="text/javascript" src="objs/maison3roof.js"></script>
	<script type="text/javascript" src="objs/maison3wood.js"></script>
	
	<script type="text/javascript" src="objs/berge.js"></script>
	<script type="text/javascript" src="objs/pont_dessus.js"></script>
	<script type="text/javascript" src="objs/pont_pierre.js"></script>
	<script type="text/javascript" src="objs/riviere.js"></script>
	<script type="text/javascript" src="objs/sol.js"></script>
	
	<script type="text/javascript" src="objs/tour1base.js"></script>
	<script type="text/javascript" src="objs/tour1mur.js"></script>
	<script type="text/javascript" src="objs/tour1parape.js"></script>
	<script type="text/javascript" src="objs/tour1sol.js"></script>
	<script type="text/javascript" src="objs/tour1toit.js"></script>

	<script type="text/javascript" src="objs/mur2base.js"></script>
	<script type="text/javascript" src="objs/mur2pierre.js"></script>
	<script type="text/javascript" src="objs/mur2bois.js"></script>
	<script type="text/javascript" src="objs/mur2toit.js"></script>
	
	<!-- load "modules" -->
	<script type="text/javascript" src="atomicGLContext.js"></script>
	<script type="text/javascript" src="atomicGLMatrixStack.js"></script>
	<script type="text/javascript" src="atomicGLTexture.js"></script>
	
	<script type="text/javascript" src="atomicGLClock.js"></script>

	<script type="text/javascript" src="atomicGLShader.js"></script>
	<script type="text/javascript" src="atomicGLwaveShader.js"></script>

	<script type="text/javascript" src="atomicGLObj.js"></script>
	<script type="text/javascript" src="atomicGLCube.js"></script>
	<script type="text/javascript" src="atomicGLxyPlane.js"></script>
	<script type="text/javascript" src="atomicGLxzPlane.js"></script>
	<script type="text/javascript" src="atomicGLSphere.js"></script>
	<script type="text/javascript" src="atomicGLCylinder.js"></script>

	<!-- SHADER: vertex color -->
	<!-- display the color of the  object -->
	<script id="vertex_vertex-color" type="x-shader/x-vertex">
	// vertex shader
	// ------------------------------------------------------
	// vertex color
	// ------------------------------------------------------

	// attributes inputs 
	attribute vec3 aVertexPosition;
	attribute vec3 aVertexNormal;
	attribute vec3 aVertexColor;

	// uniform matrices
	uniform mat4 uMVMatrix;
	uniform mat4 uPMatrix;
	uniform mat3 uNMatrix;

	// uniform ambiant color
	uniform vec3 uAmbientColor;

	// output (to fragment shader)
	varying vec3 vcolor;

	void main(void) {
		// model -> view 
		vec4 mvPosition = uMVMatrix * vec4(aVertexPosition, 1.0);
		gl_Position = uPMatrix * mvPosition;
		
		// vcolor
		vcolor = uAmbientColor+aVertexColor;
	}
</script>
	<script id="frag_vertex-color" type="x-shader/x-fragment">
	#ifdef GL_ES
		precision highp float;
	#endif

	// fragment shader
	// ------------------------------------------------------
	// vertex color
	// ------------------------------------------------------

	// varying input
	varying vec3 vcolor;

	// main
	void main(void) { gl_FragColor = vec4(vcolor, 1.0);}

</script>

	<!-- SHADER: diffus per fragment -->
	<!-- diffuse shader -->
	<script id="vertex_diffuse" type="x-shader/x-vertex">
	// vertex shader
	// ------------------------------------------------------
	// diffuse shader
	// per fragment 
	// lights nb: 1
	// ------------------------------------------------------

	// attributes inputs  (vertex arrays)
		attribute vec3 aVertexPosition;
		attribute vec3 aVertexNormal;
		attribute vec3 aVertexColor;

	// uniform matrices
		uniform mat4 uMVMatrix;
		uniform mat4 uPMatrix;
		uniform mat3 uNMatrix;

	// output (to fragment shader)
		varying vec4 vPosition;
		varying vec3 vNormal;
		varying vec3 vcolor;

	void main(void) {
		// model -> view 
		vPosition = uMVMatrix * vec4(aVertexPosition, 1.0);
		gl_Position = uPMatrix * vPosition;
		// vNormal
		vNormal = uNMatrix * aVertexNormal;	
		// vcolor
		vcolor = aVertexColor;
	}
</script>
	<script id="frag_diffuse" type="x-shader/x-fragment">
	#ifdef GL_ES
		precision highp float;
	#endif

	// fragment shader
	// ------------------------------------------------------
	// diffuse shader
	// per fragment 
	// lights nb: 1
	// ------------------------------------------------------

	// varying input (output of the vertex shader)
		varying vec4 vPosition;
		varying vec3 vNormal;
		varying vec3 vcolor;
		
	// lights
		// ambient
		uniform vec3 uAmbientColor;

		//point light ie omni dir:  location, color
		uniform vec3 uPointLightPosition0;
		uniform vec3 uPointLightColor0;

	// local constant
		// Kd: diffuse coeff
		float Kd = 1.0 ;
	
	// main
    void main(void) {	
		// light direction
		vec3 lightDirection = normalize(uPointLightPosition0 - vPosition.xyz);
		// eye direction
		vec3 eyeDirection = normalize(-vPosition.xyz);

		// normalize normal
		vec3 normal = normalize(vNormal);
		//  diffuse term
		float diffuseLightWeight = max(dot(normal, lightDirection), 0.0);
			
		// light color
         vec3 lightColor = uAmbientColor + Kd*diffuseLightWeight*uPointLightColor0 *vcolor  ;

        gl_FragColor = vec4(lightColor,1.0);
    }
</script>

	<!-- SHADER: phong per fragment -->
	<!-- phong shader -->
	<script id="vertex_phong" type="x-shader/x-vertex">
	// vertex shader
	// ------------------------------------------------------
	// phong shader
	// per fragment 
	// lights nb: 1
	// ------------------------------------------------------

	// attributes inputs  (vertex arrays)
		attribute vec3 aVertexPosition;
		attribute vec3 aVertexNormal;
		attribute vec3 aVertexColor;

	// uniform matrices
		uniform mat4 uMVMatrix;
		uniform mat4 uPMatrix;
		uniform mat3 uNMatrix;

	// output (to fragment shader)
		varying vec4 vPosition;
		varying vec3 vNormal;
		varying vec3 vcolor;

	void main(void) {
		// model -> view 
		vPosition = uMVMatrix * vec4(aVertexPosition, 1.0);
		gl_Position = uPMatrix * vPosition;
		// vNormal
		vNormal = uNMatrix * aVertexNormal;	
		// vcolor
		vcolor = aVertexColor;
	}
	</script>
	<script id="frag_phong" type="x-shader/x-fragment">
	#ifdef GL_ES
		precision highp float;
	#endif

	// fragment shader
	// ------------------------------------------------------
	// phong shader
	// per fragment 
	// lights nb: 1
	// ------------------------------------------------------

	// varying input (output of the vertex shader)
		varying vec4 vPosition;
		varying vec3 vNormal;
		varying vec3 vcolor;
		
	// lights
		// ambient
		uniform vec3 uAmbientColor;

		//point light ie omni dir:  location, color
		uniform vec3 uPointLightPosition0;
		uniform vec3 uPointLightColor0;

	// local constant
		// Kd: diffuse coeff
		float Kd = 0.5 ;
		float Ks = 0.5 ;
		float sh = 8.0 ;
		// specular color
		vec3 specColor = vec3(1.0,1.0,1.0);
	
	// main
    void main(void) {	
		// light direction
		vec3 lightDirection = normalize(uPointLightPosition0 - vPosition.xyz);
		
		// eye direction
		vec3 eyeDirection = normalize(-vPosition.xyz);

		// normalize normal
		vec3 normal = normalize(vNormal);
		
		// reflection direction
		vec3 reflectDirection = normalize(reflect(-lightDirection,normal));
		
		//  diffuse term
		float diffuseLightWeight = max(dot(normal, lightDirection), 0.0);
			
		// specular term
		float specularLightWeight = pow(max(dot(reflectDirection,eyeDirection),0.0),sh);
			
		// light color
         vec3 lightColor = uAmbientColor + Kd*diffuseLightWeight*uPointLightColor0 *vcolor + Ks*specularLightWeight*uPointLightColor0*specColor ;

        gl_FragColor = vec4(lightColor,1.0);
    }
	</script>	
	
	<!-- SHADER: cartoon per fragment -->
	<!-- cartoon shader -->
	<script id="vertex_cartoon" type="x-shader/x-vertex">
	// vertex shader
	// ------------------------------------------------------
	// cartoon shader
	// per fragment 
	// lights nb: 1
	// ------------------------------------------------------

	// attributes inputs  (vertex arrays)
		attribute vec3 aVertexPosition;
		attribute vec3 aVertexNormal;
		attribute vec3 aVertexColor;

	// uniform matrices
		uniform mat4 uMVMatrix;
		uniform mat4 uPMatrix;
		uniform mat3 uNMatrix;

	// output (to fragment shader)
		varying vec4 vPosition;
		varying vec3 vNormal;
		varying vec3 vcolor;

	void main(void) {
		// model -> view 
		vPosition = uMVMatrix * vec4(aVertexPosition, 1.0);
		gl_Position = uPMatrix * vPosition;
		// vNormal
		vNormal = uNMatrix * aVertexNormal;	
		// vcolor
		vcolor = aVertexColor;
	}
	</script>
	<script id="frag_cartoon" type="x-shader/x-fragment">
	#ifdef GL_ES
		precision highp float;
	#endif

	// fragment shader
	// ------------------------------------------------------
	// cartoon shader
	// per fragment 
	// lights nb: 1
	// ------------------------------------------------------

	// varying input (output of the vertex shader)
		varying vec4 vPosition;
		varying vec3 vNormal;
		varying vec3 vcolor;
		
	// lights
		// ambient
		uniform vec3 uAmbientColor;

		//point light ie omni dir:  location, color
		uniform vec3 uPointLightPosition0;
		uniform vec3 uPointLightColor0;

	// local constant - could be uniform
		// edge color
		vec3 edgeColor = vec3(0.0,0.0,0.0);
		// shadow color
		vec3 shadowColor = vec3(0.0, 0.0, 0.5);
		// midtoneColor
		vec3 midtoneColor = vec3(0.0,0.0,1.0);
		// highlight color
		vec3 highLightColor = vec3(1.0,1.0, 1.0);
		// diffuse scpecular shininess
		float Kd = 0.5 ;
		float Ks = 0.5 ;
		float sh = 8.0 ;
		// thresholds
		float shadowMidtone = 0.0;
		float midToneHighLight = 0.75 ;
		float edgeThreshold = 0.95 ; 
	
	// main
    void main(void) {	
		// light direction
		vec3 lightDirection = normalize(uPointLightPosition0 - vPosition.xyz);
		
		// eye direction
		vec3 eyeDirection = normalize(-vPosition.xyz);

		// normalize normal
		vec3 normal = normalize(vNormal);
		
		// reflection direction
		vec3 reflectDirection = normalize(reflect(-lightDirection,normal));
		
		//  diffuse term
		float diffuse = max(dot(normal, lightDirection), 0.0);
			
		// specular term
		float specular = pow(max(dot(reflectDirection,eyeDirection),0.0),sh);
		
		// light
		float light = Kd*diffuse + Ks*specular;
		
		// edge term
		float edge = sqrt(1.0 - pow(max(dot(normal,eyeDirection),0.0),2.0));
			
		// color
         vec3 final = vec3(1.0,0.0,0.0);
         if (edge > edgeThreshold)			
         	{ final = edgeColor;}
         else if (light <= shadowMidtone) 	
         	{ final = shadowColor;}
         else if (light <= midToneHighLight)
         	{ final = midtoneColor;}
         else 								
         	{ final = highLightColor;}

        gl_FragColor = vec4(final,1.0);
    }
	</script>	


	<!-- SHADER: texture per fragment -->
	<!-- texture shader -->
	<script id="vertex_texture" type="x-shader/x-vertex">
	// vertex shader
	// ------------------------------------------------------
	// texture shader per fragment 
	// ------------------------------------------------------

	// attributes inputs  (vertex arrays)
		attribute vec3 aVertexPosition;
		attribute vec2 aVertexTexCoord;

	// uniform matrices
		uniform mat4 uMVMatrix;
		uniform mat4 uPMatrix;
		uniform mat3 uNMatrix;

	// output
    varying vec2 vTextureCoord;


    void main(void) {
        gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
        vTextureCoord = aVertexTexCoord;
    }
	</script>
	<script id="frag_texture" type="x-shader/x-fragment">
	#ifdef GL_ES
		precision highp float;
	#endif
	// fragment shader
	// ------------------------------------------------------
	// texture shader per fragment 
	// ------------------------------------------------------

    varying vec2 vTextureCoord;
    uniform sampler2D uSampler0;
	uniform sampler2D uSampler1;

    void main(void) {
        gl_FragColor = texture2D(uSampler0, vec2(vTextureCoord.s, vTextureCoord.t));
    }
	</script>

	
		<!-- SHADER: texture per fragment -->
	<!-- texture shader -->
	<script id="vertex_texture_normal" type="x-shader/x-vertex">
	// vertex shader
	// ------------------------------------------------------
	// texture shader per fragment 
	// ------------------------------------------------------

	// attributes inputs  (vertex arrays)
		attribute vec3 aVertexPosition;
		attribute vec2 aVertexTexCoord;
		attribute vec3 aVertexNormal;
		
	//point light ie omni dir:  location, color
		uniform vec3 uPointLightPosition0;
		uniform vec3 uPointLightColor0;

	// uniform matrices
		uniform mat4 uMVMatrix;
		uniform mat4 uPMatrix;
		uniform mat3 uNMatrix;

	// output
		varying vec2 vTextureCoord;
		varying vec3 vNormal;
		varying vec4 vPosition;


    void main(void) {
		vPosition = uMVMatrix * vec4(aVertexPosition, 1.0);
        gl_Position = uPMatrix * vPosition;
        vTextureCoord = aVertexTexCoord;
		vNormal = aVertexNormal;
    }
	</script>
	<script id="frag_texture_normal" type="x-shader/x-fragment">
	#ifdef GL_ES
		precision highp float;
	#endif
	// fragment shader
	// ------------------------------------------------------
	// texture shader per fragment 
	// ------------------------------------------------------

    varying vec2 vTextureCoord;
	varying vec3 vNormal;
	varying vec4 vPosition;
	
    uniform sampler2D uSampler0;
	uniform sampler2D uSampler1;
	
	//point light ie omni dir:  location, color
	uniform vec3 uPointLightPosition0;
	uniform vec3 uPointLightColor0;
	
	float Kd = 1.0;

    void main(void) {
		vec3 lightDirection = normalize(uPointLightPosition0 - vPosition.xyz);
		
		//vec3 normal = normalize(texture2D(uSampler1,vTextureCoord).xyz);
		
		vec3 normal = normalize(vNormal);
		
		float diffuseLightWeight = max(dot(normal, lightDirection), 0.0);
		vec3 lightColor = Kd*diffuseLightWeight*texture2D(uSampler0, vTextureCoord).xyz;
        gl_FragColor = vec4(lightColor,1.0);
    }
	</script>
	
	<!-- SHADER: wave vertex -->
	<!-- wave shader -->
	<script id="vertex_wave" type="x-shader/x-vertex">
	// vertex shader
	// ------------------------------------------------------
	// wave vertex shader
	// sum of 4 sin waves
	// ------------------------------------------------------

	// attributes inputs  (vertex arrays)
		attribute vec3 aVertexPosition;
		attribute vec3 aVertexNormal;
		attribute vec2 aVertexTexCoord;
		
	// uniform waves
	// A sin(w*u + p*t)
	// [0]: A amplitude
	// [1]: w omega
	// [2]: p phi
		uniform vec3 uA0 ;
		uniform vec3 uA1 ;
		uniform vec3 uA2 ;
		uniform vec3 uA3 ;

	// uniform
		uniform vec2 uXrange;
		
	// uniform time
		uniform float utime ;

	// uniform matrices
		uniform mat4 uMVMatrix;
		uniform mat4 uPMatrix;
		uniform mat3 uNMatrix;

	// output (to fragment shader)
		varying vec4 vPosition;
		varying vec3 vNormal;
		varying vec3 vcolor;
		
	// output texture
		varying vec2 vTextureCoord;

	void main(void) {
		float u = (aVertexPosition.x - uXrange.x)/(uXrange.y -uXrange.x)*6.28 ;
		float wupt0 = uA0.y*u+uA0.z*utime ;
		float wupt1 = uA1.y*u+uA1.z*utime ;
		float wupt2 = uA2.y*u+uA2.z*utime ;
		float wupt3 = uA3.y*u+uA3.z*utime ;
		// Asin
		float Asin0 = uA0.x*sin(wupt0);
		float Asin1 = uA1.x*sin(wupt1);
		float Asin2 = uA2.x*sin(wupt2);
		float Asin3 = uA3.x*sin(wupt3);
		
		float dy = Asin0 +Asin1 +Asin2 +Asin3 ;
		// tangent
		float tx =1.0;
		float ty0 = uA0.x*6.28*uA0.y*cos(wupt0)/(uXrange.y -uXrange.x);
		float ty1 = uA1.x*6.28*uA1.y*cos(wupt1)/(uXrange.y -uXrange.x);
		float ty2 = uA2.x*6.28*uA2.y*cos(wupt2)/(uXrange.y -uXrange.x);
		float ty3 = uA3.x*6.28*uA3.y*cos(wupt3)/(uXrange.y -uXrange.x);
		float ty = ty0 + ty1 + ty2+ ty3;
		float tz =0.0 ;
		// normal
		vec3 dnormal= normalize(vec3(-ty,tx,tz));
		// model -> view 
		vPosition = uMVMatrix * vec4(aVertexPosition+vec3(0.0,dy,0.0), 1.0);
		gl_Position = uPMatrix * vPosition;
		// vNormal
		vNormal = uNMatrix * (vec3(vNormal.x,0.0,vNormal.z)+dnormal);	
		// vTextureCoord
		vTextureCoord = aVertexTexCoord;
	}
</script>

	<!-- ATOMICGL ENGINE -->
	<!-- create objects -->
	<!-- wegGLStart function (required) -->
	<script type="text/javascript">
	//------------------------------------------------------------------------------

	// OpenGL context
	var agl = new atomicGLContext();
	
	// clock
	var sceneClock =new atomicGLClock() ;

	// matrix stack
	var ams =new atomicGLMatrixStack();
	
	// texture
	var mur_pierre1 ; 
	var toit1 ;
	var toit1_normalMap;
	var bois1 ;
	var eau1 ;
	var pave2 ;
	var pierre2;
	
	
	// shader and id
	var textProg ; var textProgId ;
	var waveProg ; var waveProgId ;

	// scene
	var house3 		= new atomicGLObj('maison3', 		new maison3(),		1.0,1.0) ;
	var house3roof 	= new atomicGLObj('maison3roof', 	new maison3roof(),	1.0,1.0) ;
	var house3wood 	= new atomicGLObj('maison3wood', 	new maison3wood(),	1.0,1.0) ;
	var bridge1stone= new atomicGLObj('bridge1stone', 	new pont_pierre(),	1.0,1.0) ;
	var bridge1top 	= new atomicGLObj('bridge1top', 	new pont_dessus(),	0.1,0.1) ;
	var ground0top 	= new atomicGLObj('ground0top', 	new sol(),			0.1,0.1) ;
	var ground0edge = new atomicGLObj('ground0edge', 	new berge(),		1.0,1.0) ;
	var river1 		= new atomicGLObj('river1', 		new riviere(),		1.0,1.0) ;
	
	var tower1base	= new atomicGLObj('tou1base', 		new tour1base(),	0.2,0.2) ;
	var tower1mur	= new atomicGLObj('tour1mur', 		new tour1mur(),		1.0,1.0) ;
	var tower1parape = new atomicGLObj('tour1parape', 	new tour1parape(),	1.0,1.0) ;
	var tower1sol	= new atomicGLObj('tour1sol', 		new tour1sol(),		1.0,1.0) ;
	var tower1toit 	= new atomicGLObj('tour1toit', 		new tour1toit(),	1.0,1.0) ;

	var wall2base	= new atomicGLObj('wall1base', 		new mur2base(),		0.2,0.2) ;
	var wall2stone 	= new atomicGLObj('wall2stone', 	new mur2pierre(),	1.0,1.0) ;
	var wall2wood	= new atomicGLObj('wall2wood', 		new mur2bois(),		1.0,1.0) ;
	var wall2roof 	= new atomicGLObj('wall2roof', 		new mur2toit(),	1.0,1.0) ;
	
	var water 		= new atomicGLxzPlane("water",15,50,200,2,15.0,10.0);

	// animation
	var Yangle = 0.0 ;
	var waveTime = 0.0 ;
	

	//webGLStart
	function webGLStart() {
	// init
	// -----------------------------
		// recover penGL canvas 
		var canvas = document.getElementById("oglcanvas");

		// init OpenGL context
		agl.initGL(canvas,[1.0,1.0,1.0]);
		
		// light
		agl.pushLight([50.0,50.0,-10.0],[1.0,1.0,1.0]); // position, color
		agl.ambientLightColor = [0.01,0.01,0.2];	// color

		// init Matrix Stack
		ams.initMatrix(agl,45); // fov = 45 degrees
		
		// texture
		mur_pierre1 =  	new atomicGLTexture("texture/mur_pierre1.png","color",agl);
		toit1 		=  	new atomicGLTexture("texture/toit1.png","color",agl);
		toit1_normalMap = new atomicGLTexture("texture/toit1_normalMap.png","color",agl);
		bois1 		=  	new atomicGLTexture("texture/bois1.png","color",agl);
		pave2  		=  	new atomicGLTexture("texture/pave2.png","color",agl);
		eau1 		=	new atomicGLTexture("texture/eau1.png","color",agl);
		pierre2 	= 	new atomicGLTexture("texture/pierre2.png","color",agl);
		pierre3 	= 	new atomicGLTexture("texture/pierre3.png","color",agl);
		pierre3_normalMap = new atomicGLTexture("texture/pierre3_normalMap.png","color",agl);
		sol3 		= 	new atomicGLTexture("texture/sol3.png","color",agl);

		// load shaders and push shader in context	
		textProg 		= new atomicGLShader("texture", agl,"frag_texture", "vertex_texture" ,1,0);
		textProgId		= agl.pushProgram(textProg);
		
		textProgNormal 	= new atomicGLShader("texture_normal",agl,"frag_texture_normal","vertex_texture_normal",2,1);
		textProgNormalId= agl.pushProgram(textProgNormal);

		wtextProg 		= new atomicGLwaveShader("wave_texture", agl,"frag_texture", "vertex_wave" ,1,0);
		wtextProgId		= agl.pushProgram(wtextProg);
		// wave param
		wtextProg.A0 = [0.20,1.0,1.0];
		wtextProg.A1 = [0.20,2.0,1.0]; 
		wtextProg.A2 = [0.10,3.0,1.0]; 
		wtextProg.A3 = [0.10,4.0,1.0]; 
		wtextProg.xRange = [-5.0,5.0]; 
		// wave animation 
		wtextProg.wTime = 0.0 ;	
		
		// house 3: texture and init
		house3.pushTexture(mur_pierre1);
		house3.initGLBuffers(agl);
		
		house3roof.pushTexture(toit1);
		house3roof.pushTexture(toit1_normalMap);
		house3roof.initGLBuffers(agl);
		
		house3wood.pushTexture(bois1);
		house3wood.initGLBuffers(agl);
		
		// bridge
		bridge1stone.pushTexture(mur_pierre1); 	bridge1stone.initGLBuffers(agl);
		bridge1top.pushTexture(pave2); 			bridge1top.initGLBuffers(agl);
		
		// ground
		ground0top.pushTexture(pave2); 			ground0top.initGLBuffers(agl);
		ground0edge.pushTexture(pierre2); 		ground0edge.initGLBuffers(agl);
		river1.pushTexture(eau1); 				river1.initGLBuffers(agl);
		
		// tower
		tower1base.pushTexture(pierre3);		tower1base.initGLBuffers(agl);
		tower1mur.pushTexture(mur_pierre1);		tower1mur.initGLBuffers(agl);
		tower1parape.pushTexture(bois1);		tower1parape.initGLBuffers(agl);
		tower1sol.pushTexture(sol3);			tower1sol.initGLBuffers(agl);
		tower1toit.pushTexture(toit1);			tower1toit.initGLBuffers(agl);
		
		// wall
		wall2base.pushTexture(pierre3) ;		wall2base.initGLBuffers(agl);
		wall2stone.pushTexture(mur_pierre1);	wall2stone.initGLBuffers(agl);
		wall2wood.pushTexture(bois1);			wall2wood.initGLBuffers(agl);
		wall2roof.pushTexture(toit1);			wall2roof.initGLBuffers(agl);
		
		// 	water
		water.pushTexture(eau1) ;water.initGLBuffers(agl);

		// start the animation
		nextFrame();
	}
		
	// draw
	// -----------------------------
	function sceneDraw(){
		agl.initDraw();

		// global transform
		// ------------------------------
		// push matrix
		ams.mvPushMatrix();
		// position & orientation	

			ams.mvTranslate(0,-2,-40);
			ams.mvRotate(20,[1,0,0]);

			// scene 
			// ---------------------------------
			// push matrix
			ams.mvPushMatrix();
			// position & orientation
				ams.mvTranslate(0.0,0.0,0.0);
				ams.mvRotate(-45.0+Yangle,[0,1,0]);
				
				// tower
				// ---------------------------------
				// push matrix
				ams.mvPushMatrix();
					// position & orientation
					ams.mvTranslate(-25.0,0,-10);
					// draw
					tower1base.draw(agl,ams,textProgNormalId);
					tower1mur.draw(agl,ams,textProgNormalId);
					tower1parape.draw(agl,ams,textProgNormalId);
					tower1sol.draw(agl,ams,textProgNormalId);
					tower1toit.draw(agl,ams,textProgNormalId);
					// wall
					// push matrix
					ams.mvPushMatrix();
						// position & orientation
						ams.mvTranslate(4.4,0,-1.0);
						// draw
						wall2base.draw(agl,ams,textProgNormalId);
						wall2stone.draw(agl,ams,textProgNormalId);
						wall2wood.draw(agl,ams,textProgNormalId);
						wall2roof.draw(agl,ams,textProgNormalId);
						// next part
						ams.mvTranslate(19.5,0,0.0);
						// draw
						wall2base.draw(agl,ams,textProgNormalId);
						wall2stone.draw(agl,ams,textProgNormalId);
						wall2wood.draw(agl,ams,textProgNormalId);
						wall2roof.draw(agl,ams,textProgNormalId);
					// pop matrix
					ams.mvPopMatrix();
					// draw
				// pop matrix
				ams.mvPopMatrix();				
			
				// river
				// ---------------------------------
				// push matrix
				ams.mvPushMatrix();
					// position & orientation
					ams.mvTranslate(0.0,-3.5,8.0);
					// draw
					water.draw(agl,ams,wtextProgId);
				// pop matrix
				ams.mvPopMatrix();
			
				// ground
				// ----------------------------
				ground0top.draw(agl,ams,textProgNormalId);
				ground0edge.draw(agl,ams,textProgNormalId);
				river1.draw(agl,ams,textProgNormalId);

				
				// house3 and bridge
				// ---------------------------------------
				// push matrix
				ams.mvPushMatrix();
					// position & orientation
					ams.mvTranslate(0.0,0.1,0.0);

					// draw
					house3.draw(agl,ams,textProgNormalId);
					house3roof.draw(agl,ams,textProgNormalId);
					house3wood.draw(agl,ams,textProgNormalId);
					
					bridge1stone.draw(agl,ams,textProgNormalId);
					bridge1top.draw(agl,ams,textProgNormalId);
					
					// pop matrix
				ams.mvPopMatrix();
			
			// pop matrix
			ams.mvPopMatrix();

		// ---------------------------------------	
		// pop matrix
		ams.mvPopMatrix();
	}
	
	// nextFrame
	// -----------------------------
    function nextFrame() {
        requestAnimFrame(nextFrame);
        sceneDraw();
        animate();
    }
    
    // animate
    // ------------------------------
    function animate(){
    	// increase time
    	sceneClock.tick() ;
    	Yangle = Yangle + 0.01*sceneClock.get();
		waveTime = waveTime + 0.005*sceneClock.get();
    	wtextProg.wTime = waveTime ;
    }

</script>
</head>

<body onload="webGLStart();">
	<br>
    <canvas id="oglcanvas" style="border: none;" width="1600" height="900"></canvas>
	<br>
</body></html>
